group 'com.dmgburg'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.2-2'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.jetbrains:gradle-intellij-plugin:0.1.10"
    }
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'kotlin'

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/gen', 'src/main/jflex', "$buildDir/grammar/"]
        }
    }
}

def ideaVersion = '2017.1'

intellij {
    version "IC-$ideaVersion"
}

repositories {
    mavenCentral()
    maven {
        url = "https://www.jetbrains.com/intellij-repository/releases"
    }
}

configurations {
    ideaRelease
    jflex
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    ideaRelease "com.jetbrains.intellij.idea:ideaIC:$ideaVersion"
//    jflex group: 'de.jflex', name: 'jflex', version: '1.7.0'
}

task generationJars(type: Sync) {
    dependsOn configurations.ideaRelease

    from { // use of closure defers evaluation until execution time
        configurations.ideaRelease.collect {zipTree(it).matching{ it.include("lib/*")}.files}
    }
    into "$buildDir/grammarKit/"
}

task generateParser(type: JavaExec, dependsOn: generationJars) {
    classpath = files(fileTree("$buildDir/grammarKit"), fileTree("$projectDir/grammarKit"))
    main = 'org.intellij.grammar.Main'
    args "$projectDir/src/main/gen/", "$projectDir\\src\\main\\Alfa.bnf"
}

task generateLexer(type: JavaExec) {
    classpath = fileTree("$projectDir/grammarKit")
    main = 'jflex.Main'
    args "-d", "$projectDir/src/main/gen/com/dmgburg/alfa/psi/parser", "--skel", "$projectDir/grammarKit/idea-flex.skeleton",  "$projectDir/src/main/AlfaLexer.flex"
}

//compileJava{
//    dependsOn generateLexer
//    dependsOn generateParser
//}
